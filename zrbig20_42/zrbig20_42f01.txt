*&---------------------------------------------------------------------*
*&  Include           ZRBIG20_42F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  SET_INIT_VALUE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_init_value .

  p_rbukrs  = '1000'.
  p_ryear   = sy-datum(4).
  p_rldnr   = '0L'.
  p_rvers   = '1'.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_data .

  CLEAR : gs_flext, gt_flext.
  SELECT racct drcrk SUM( hslvt ) AS hslvt
    INTO CORRESPONDING FIELDS OF TABLE gt_flext
    FROM faglflext
    WHERE rbukrs  EQ p_rbukrs
    AND   ryear   EQ p_ryear
    AND   rldnr   EQ p_rldnr
    AND   rvers   EQ p_rvers
    GROUP BY racct drcrk.

  IF gt_flext IS INITIAL.
    MESSAGE s037  DISPLAY LIKE 'E'.
    STOP.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  ADD_TEXT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_text_data .

  DATA : lt_flext LIKE TABLE OF gs_flext.

*-- Copy본 생성
  lt_flext  = gt_flext.

**********************************************************************
* For all entries In
**********************************************************************

*--1st . data가 있는지 확인 ( 위에서 이미 했기 때문에 생략 )
*--2nd . Sort
  SORT lt_flext BY racct ASCENDING.

*--3rd . Delete duplicate data
  DELETE ADJACENT DUPLICATES FROM lt_flext COMPARING racct.

  CLEAR : gs_skat, gt_skat.
  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE gt_skat
    FROM skat
    FOR ALL ENTRIES IN lt_flext
    WHERE saknr EQ lt_flext-racct
    AND   spras EQ sy-langu
    AND   ktopl EQ 'CAIN'.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM display_data .


  SORT gt_skat BY saknr ASCENDING.

  CLEAR : gs_body, gt_body, gs_flext.
  LOOP AT gt_flext INTO gs_flext.

    gs_body-racct = gs_flext-racct.

    CASE gs_flext-drcrk.
      WHEN 'S'.
        gs_body-debit  = gs_flext-hslvt.
      WHEN 'H'.
        gs_body-credit = gs_flext-hslvt.
    ENDCASE.

    CLEAR : gs_skat.
    READ TABLE gt_skat INTO gs_skat WITH KEY saknr = gs_flext-racct
        BINARY SEARCH.

    gs_body-txt50 = gs_skat-txt50.

    COLLECT gs_body INTO gt_body.
    CLEAR   gs_body.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_CONTROL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM display_control .

  PERFORM set_layo_sort.

  CLEAR : gs_fcat, gt_fcat.
  PERFORM set_field_catalog USING :
        "pk   "FieldName        "Col text           "Emphasize
        ' '     'DEBIT'         'Debit'             ' ',
        ' '     'RACCT'         'G/L Account'       ' ',
        ' '     'TXT50'         'G/L Text'          'X',
        ' '     'CREDIT'        'Credit'            ' '.

  IF go_container IS INITIAL.
    PERFORM create_obj.

    SET HANDLER lcl_event_handler=>on_hotspot_click FOR go_alv_grid.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_save                        = 'A'
        i_default                     = 'X'
        is_layout                     = gs_layout
      CHANGING
        it_outtab                     = gt_body
        it_fieldcatalog               = gt_fcat
*       it_sort                       =
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        OTHERS                        = 4.

  ELSE.
    CALL METHOD go_alv_grid->refresh_table_display.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_LAYO_SORT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_layo_sort .

  CLEAR : gs_layout, gs_sort, gt_sort.
  gs_layout-zebra       = 'X'.
  gs_layout-sel_mode    = 'D'.
  gs_layout-cwidth_opt  = 'X'.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_FIELD_CATALOG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_0195   text
*      -->P_0196   text
*      -->P_0197   text
*      -->P_0198   text
*----------------------------------------------------------------------*
FORM set_field_catalog  USING pv_key pv_fname pv_coltext pv_emphasize.

  gs_fcat-key       = pv_key.
  gs_fcat-fieldname = pv_fname.
  gs_fcat-coltext   = pv_coltext.
  gs_fcat-emphasize = pv_emphasize.

  CASE pv_fname.
    WHEN 'RACCT'.
      gs_fcat-hotspot   = 'X'.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR  gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  CREATE_OBJ
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM create_obj .

  CREATE OBJECT go_container
    EXPORTING
      repid     = sy-repid
      dynnr     = sy-dynnr
      side      = go_container->dock_at_left
      extension = 3000.

  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_container.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  ON_HOTSPOT_CLICK
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_E_ROW_ID  text
*----------------------------------------------------------------------*
FORM on_hotspot_click  USING    pv_row_id.

  CLEAR gs_body.
  READ TABLE gt_body INTO gs_body INDEX pv_row_id.

  SET PARAMETER ID : 'ACC' FIELD gs_body-racct,
                     'BUK' FIELD p_rbukrs,
                     'GJR' FIELD p_ryear.

  CALL TRANSACTION 'FAGLB03' AND SKIP FIRST SCREEN.

ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 750

*&---------------------------------------------------------------------*
*&  Include           ZRBIG20_60F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  SET_INIT_VALUE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_init_value .

  p_bukrs = '1000'.
  p_gjahr = sy-datum(4).

  s_blart-sign   = 'I'.
  s_blart-option = 'EQ'.
  s_blart-low    = 'SA'.
  APPEND s_blart.
  CLEAR  s_blart.

  s_blart-sign   = 'I'.
  s_blart-option = 'EQ'.
  s_blart-low    = 'AB'.
  APPEND s_blart.
  CLEAR  s_blart.

  s_monat-sign   = 'I'.
  s_monat-option = 'BT'.
  s_monat-low    = '1'.
  s_monat-high   = sy-datum+4(2).
  APPEND s_monat.
  CLEAR  s_monat.

  s_budat-sign   = 'I'.
  s_budat-option = 'BT'.
  s_budat-low    = '20180101'.
  s_budat-high   = sy-datum.
  APPEND s_budat.
  CLEAR  s_budat.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_data .

  DATA : lt_bset LIKE gt_bset.

  CLEAR : gt_bkpf.
  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE gt_bkpf
    FROM bkpf
    WHERE bukrs EQ p_bukrs
    AND   gjahr EQ p_gjahr
    AND   blart IN s_blart
    AND   monat IN s_monat
    AND   budat IN s_budat.

  CLEAR : gt_bset.
  SELECT *
   INTO CORRESPONDING FIELDS OF TABLE gt_bset
   FROM bset
   WHERE bukrs EQ p_bukrs
   AND   gjahr EQ p_gjahr.

  IF gt_bset IS NOT INITIAL.

    CLEAR : lt_bset.
    lt_bset = gt_bset.

    SORT lt_bset BY hkont ASCENDING.
    DELETE ADJACENT DUPLICATES FROM lt_bset COMPARING hkont.

    CLEAR : gt_skat.
    SELECT *
      INTO CORRESPONDING FIELDS OF TABLE gt_skat
      FROM skat
      FOR ALL ENTRIES IN lt_bset
      WHERE saknr EQ lt_bset-hkont
      AND   spras EQ sy-langu.

    CLEAR : lt_bset.
    lt_bset = gt_bset.

    SORT lt_bset BY mwskz ASCENDING.
    DELETE ADJACENT DUPLICATES FROM lt_bset COMPARING mwskz.

    CLEAR : gt_t007s.
    SELECT *
      INTO CORRESPONDING FIELDS OF TABLE gt_t007s
      FROM t007s
      FOR ALL ENTRIES IN lt_bset
      WHERE mwskz EQ lt_bset-mwskz
      AND   spras EQ sy-langu.

  ELSE.
    MESSAGE s037 DISPLAY LIKE 'E'.
    STOP.
  ENDIF.




ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  MAKE_DISPLAY_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM make_display_data .

  SORT : gt_skat  BY saknr ASCENDING,
         gt_t007s BY mwskz ASCENDING.

  CLEAR : gs_bkpf,gt_body.
  LOOP AT gt_bkpf INTO gs_bkpf.

    CLEAR : gs_bset.
    LOOP AT gt_bset INTO gs_bset WHERE bukrs EQ gs_bkpf-bukrs
                                 AND   belnr EQ gs_bkpf-belnr
                                 AND   gjahr EQ gs_bkpf-gjahr.

      CLEAR : gs_body.
      MOVE-CORRESPONDING : gs_bset TO gs_body,
                           gs_bkpf TO gs_body.

      CLEAR : gs_skat.
      READ TABLE gt_skat INTO gs_skat WITH KEY saknr = gs_body-hkont
                                      BINARY SEARCH.
      gs_body-txt50 = gs_skat-txt50.

      CLEAR : gs_t007s.
      READ TABLE gt_t007s INTO gs_t007s WITH KEY mwskz = gs_body-mwskz
                                        BINARY SEARCH.
      gs_body-text1 = gs_t007s-text1.

      APPEND gs_body TO gt_body.
      CLEAR  gs_body.

    ENDLOOP.


  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SAVE_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM save_data .

  DATA : ls_save  TYPE ztbig20_19,
         lt_save  LIKE TABLE OF ls_save,
         ls_check TYPE ztbig20_19,
         lv_anwer.

  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar              = 'SAVE DATA'
      text_question         = ' SAVE Go Go?'
      text_button_1         = TEXT-y01
      text_button_2         = TEXT-n01
      display_cancel_button = 'X'
    IMPORTING
      answer                = lv_anwer.

  IF lv_anwer NE '1'.
    EXIT.
  ENDIF.

  CLEAR : gs_body, lt_save.
  LOOP AT gt_body INTO gs_body.

    CLEAR : ls_save.
    MOVE-CORRESPONDING gs_body TO ls_save.
    ls_save-bukrs = p_bukrs.
    ls_save-gjahr = p_gjahr.

    CLEAR : ls_check.
    SELECT SINGLE *
      INTO CORRESPONDING FIELDS OF ls_check
      FROM ztbig20_19
      WHERE bukrs = ls_save-bukrs
      AND   gjahr = ls_save-gjahr
      AND   belnr = ls_save-belnr.

    IF sy-dbcnt GT 0.
      ls_save-erdat = ls_check-erdat.
      ls_save-ernam = ls_check-ernam.
      ls_save-erzet = ls_check-erzet.
      ls_save-aedat = sy-datum.
      ls_save-aenam = sy-uname.
      ls_save-aezet = sy-uzeit.
    ELSE.
      ls_save-erdat = sy-datum.
      ls_save-ernam = sy-uname.
      ls_save-erzet = sy-uzeit.
    ENDIF.

    APPEND ls_save TO lt_save.

  ENDLOOP.

  MODIFY ztbig20_19 FROM TABLE lt_save.

  IF sy-subrc EQ 0.
    COMMIT WORK.
    MESSAGE s102.
  ELSE.
    ROLLBACK WORK.
    MESSAGE s001 WITH TEXT-e01 DISPLAY LIKE 'E'.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_SCREEN_100
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM display_screen_100 .

  PERFORM set_layo_sort.

  CLEAR : gs_fcat, gt_fcat.
  PERFORM set_fcat USING :
        "pk "FieldName    "Coltext        "emphasize
        'X'  'BELNR'      'Document no'    ' ',
        ' '  'BLART'      'Document type'  ' ',
        ' '  'HKONT'      'G/L Account'    ' ',
        ' '  'TXT50'      'G/L text'       'X',
        ' '  'MWSKZ'      'Tax code'       ' ',
        ' '  'TEXT1'      'Tax code name'  'X',
        ' '  'HWBAS'      'Tax amount'     ' ',
        ' '  'WAERS'      'Currency'       ' '.

  IF go_container IS INITIAL.

    PERFORM create_obj.

    SET HANDLER : lcl_event_handler=>double_click  FOR go_alv_grid,
                  lcl_event_handler=>hotspot_click FOR go_alv_grid.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        i_save                        = 'A'
        i_default                     = 'X'
        is_layout                     = gs_layo
      CHANGING
        it_outtab                     = gt_body
        it_fieldcatalog               = gt_fcat
        it_sort                       = gt_sort
*       it_filter                     =
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        OTHERS                        = 4.
  ELSE.
    CALL METHOD go_alv_grid->refresh_table_display.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_LAYO_SORT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_layo_sort .

  CLEAR : gs_layo.
  gs_layo-sel_mode    = 'D'.
  gs_layo-cwidth_opt  = 'X'.
  gs_layo-totals_bef  = 'X'.
  gs_layo-zebra       = 'X'.

  CLEAR : gs_sort, gt_sort.
  gs_sort-fieldname   = 'HKONT'.
  gs_sort-spos        = 1.
  gs_sort-subtot      = 'X'.
  APPEND gs_sort TO gt_sort.
  CLEAR  gs_sort.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_FCAT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_0480   text
*      -->P_0481   text
*      -->P_0482   text
*      -->P_0483   text
*----------------------------------------------------------------------*
FORM set_fcat  USING  pv_k  pv_f  pv_c  pv_e.

  CLEAR : gs_fcat.
  gs_fcat-key       = pv_k.
  gs_fcat-fieldname = pv_f.
  gs_fcat-coltext   = pv_c.
  gs_fcat-emphasize = pv_e.

  CASE pv_f.
    WHEN 'HWBAS'.
      gs_fcat-cfieldname  = 'WAERS'.
      gs_fcat-do_sum      = 'X'.
    WHEN 'BELNR'.
      gs_fcat-hotspot     = 'X'.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR  gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  CREATE_OBJ
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM create_obj .

  CREATE OBJECT go_container
    EXPORTING
      repid     = sy-repid
      dynnr     = sy-dynnr
      side      = go_container->dock_at_left
      extension = 3000.

  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_container.


ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  HOTSPOT_CLICK
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_E_ROW_ID  text
*----------------------------------------------------------------------*
FORM hotspot_click  USING    pv_row_id.

  DATA : lv_htype TYPE dd01v-datatype.

  CALL FUNCTION 'NUMERIC_CHECK'
    EXPORTING
      string_in = pv_row_id
    IMPORTING
      htype     = lv_htype.

  IF lv_htype EQ 'CHAR'.
    EXIT.
  ENDIF.

  CLEAR : gs_body.
  READ TABLE gt_body INTO gs_body INDEX pv_row_id.

  SET PARAMETER ID : 'BLN' FIELD gs_body-belnr,
                     'BUK' FIELD p_bukrs,
                     'GJR' FIELD p_gjahr.

  CALL TRANSACTION 'FB03' AND SKIP FIRST SCREEN.


ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  DOUBLE_CLICK
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_E_ROW  text
*----------------------------------------------------------------------*
FORM double_click  USING    pv_row.

  DATA : lv_htype TYPE dd01v-datatype,
         lv_subrc TYPE sy-subrc.

  CALL FUNCTION 'NUMERIC_CHECK'
    EXPORTING
      string_in = pv_row
    IMPORTING
      htype     = lv_htype.

  IF lv_htype EQ 'CHAR'.
    EXIT.
  ENDIF.

  CLEAR : gs_body.
  READ TABLE gt_body INTO gs_body INDEX pv_row.

  CLEAR : gt_bseg.
  CALL FUNCTION 'ZFM_DOC_DETAIL_20_10'
    EXPORTING
      i_bukrs = p_bukrs
      i_belnr = gs_body-belnr
      i_gjahr = p_gjahr
    IMPORTING
      e_subrc = lv_subrc
    TABLES
      t_bseg  = gt_bseg.

  IF lv_subrc EQ 0.
    CALL SCREEN 110 STARTING AT 03 03.
  ELSE.
    MESSAGE s037 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_SCREEN_110
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM display_screen_110 .

  PERFORM set_pop_layo.

  CLEAR : gs_pop_fcat, gt_pop_fcat.
  PERFORM set_pop_fcat USING :
        "pk "FieldName "Coltext         "emphasize
         'X'  'BUKRS'  'Company'        ' ',
         'X'  'BELNR'  'Document no'    ' ',
         'X'  'GJAHR'  'Fiscal year'    ' ',
         ' '  'BUZEI'  'Line item no'   ' ',
         ' '  'BUZID'  'Line item id'   ' ',
         ' '  'PSWBT'  'Amount'         ' ',
         ' '  'PSWSL'  'Currncy'        ' '.

  IF go_pop_con IS INITIAL.

    PERFORM create_obj_pop.

    CALL METHOD go_pop_grid->set_table_for_first_display
      EXPORTING
        i_save                        = 'A'
        i_default                     = 'X'
        is_layout                     = gs_pop_layo
      CHANGING
        it_outtab                     = gt_bseg
        it_fieldcatalog               = gt_pop_fcat
*       it_sort                       =
*       it_filter                     =
      EXCEPTIONS
        invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        OTHERS                        = 4.
  ELSE.
    CALL METHOD go_pop_grid->refresh_table_display.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_POP_LAYO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_pop_layo .

  CLEAR : gs_pop_layo.
  gs_pop_layo-sel_mode   = 'D'.
  gs_pop_layo-cwidth_opt = 'X'.
  gs_pop_layo-zebra      = 'X'.
  gs_pop_layo-totals_bef = 'X'.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_POP_FCAT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_0846   text
*      -->P_0847   text
*      -->P_0848   text
*      -->P_0849   text
*----------------------------------------------------------------------*
FORM set_pop_fcat  USING  pv_k  pv_f  pv_c  pv_e.

  gs_pop_fcat-key       = pv_k.
  gs_pop_fcat-fieldname = pv_f.
  gs_pop_fcat-coltext   = pv_c.
  gs_pop_fcat-emphasize = pv_e.

  CASE pv_f.
    WHEN 'PSWBT'.
      gs_pop_fcat-cfieldname = 'PSWSL'.
      gs_pop_fcat-do_sum     = 'X'.
  ENDCASE.

  APPEND gs_pop_fcat TO gt_pop_fcat.
  CLEAR  gs_pop_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  CREATE_OBJ_POP
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM create_obj_pop .

  CREATE OBJECT go_pop_con
    EXPORTING
      container_name = 'GO_CONT'.

  CREATE OBJECT go_pop_grid
    EXPORTING
      i_parent = go_pop_con.


ENDFORM.

----------------------------------------------------------------------------------
Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 750
